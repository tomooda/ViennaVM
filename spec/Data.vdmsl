module Data
exports all
definitions
types
     Byte = nat inv b == b < 0x100; -- 8bits unsigned integer
     Word = nat inv w == w < 0x10000; -- 16bits unsigned integer
     Dword = nat inv dw == dw < 0x100000000; -- 32bits unsigned integer
     Qword = nat inv qw == qw < 0x10000000000000000; -- 64bits unsigned integer
     OID = Qword; -- 64 bits unsigned integer
     Int = int
        inv i == i > -0x8000000000000000 and i < 0x8000000000000000; -- 63 bits signed integer
     Float :: sign : nat exponent : nat fraction : nat
        inv mk_Float(s, e, f) == (s < 2 and e < 0x100) and f < 0x1000000; -- 32 bits floating point
     Char = Dword; -- 32 bits unsigned int
     Pointer = nat inv p == p <= 0x100000000000000; -- 56 bits address

values
     /* oid tag flags */
     smallIntegerFlag = 1;
     immediateFlag = 2;
     typeFlag = 4;
     optionTypeFlag = 8;
     /* immediate value oid tags */
     smallIntegerTag = 1;
     invalidTag = 0x0a;
     floatTag = 0x12;
     charTag = 0x1a;
	  /* common value oids */
     nilValue = 0x22;
     trueValue = 0x2a;
     falseValue = 0x32;
     undefinedValue = 0x3a;
     emptySeqValue = 0x82;
     emptySetValue = 0x8a;
     emptyMapValue = 0x92;
     /* immediate type oid defs */
     unitType = 0x06;
     boolType = 0x16;
     charType = 0x26;
     realType = 0x36;
     intType = 0x46;
     natType = 0x56;
     nat1Type = 0x66;
     tokenType = 0x76;
     anyType = 0xf6;
     /* heap value oid defs */
     tupleTag = 0x0;
     recordTag = 0x10;
     tokenTag = 0x20;
     seqTag = 0x30;
     setTag = 0x40;
     mapTag = 0x50;
     quoteTag = 0x60;
     functionTag = 0x70;
     operationTag = 0x80;
     largePositiveIntTag = 0x90;
     largeNegativeIntTag = 0xa0;
     /* heap type oid defs */
     tupleType = 0x04;
     recordType = 0x14;
     seqType = 0x24;
     setType = 0x34;
     mapType = 0x44;
     quoteType = 0x54;
     functionType = 0x64;
     operationType = 0x74;
     productType = 0x84;
     unionType = 0x94;
	  /* default values */
     invalidOidValue = invalidTag;
     invalidIntValue = 0x7fffffffffffffff;
     invalidFloatValue = mk_Float(1, 255, 0x7fffff);
     invalidCharValue = 0xffffffff;
     invalidPointerValue = 0x100000000000000;

functions
    float2real : Float -> real
    float2real(x) ==
        (if x.sign = 0 then 1 else -1)
         * (if
                x.exponent = 0
            then
                x.fraction / 2 ** 149
            else
                (1 + x.fraction / 2 ** 23)  * exp(2, x.exponent - 127));
    
    real2float : real -> Float
    real2float(x) ==
        let
            a = abs x,
            f =
                if a < exp(2, -126) then denormalized_r2f(a) else normalized_r2f(a)
        in mk_Float(if x >= 0 then 0 else 1, f.exponent, f.fraction);
    
    normalized_r2f : real -> Float
    normalized_r2f(x) ==
        if x >= 2.0
            then (let f = normalized_r2f(x / 2.0)
                in mk_Float(f.sign, f.exponent + 1, f.fraction))
        elseif x < 1.0
            then let f = normalized_r2f(x  * 2.0)
            in mk_Float(f.sign, f.exponent - 1, f.fraction)
        else
            mk_Float(0, 127, floor ((x - 1)  * 2 ** 23 + 0.5))
    pre x >= 0;
    
    denormalized_r2f : real -> Float
    denormalized_r2f(x) ==
        mk_Float(0, 0, floor (x  * 2 ** (126 + 23) + 0.5))
    pre x >= 0;
    
    exp : real * real -> real
    exp(x, y) == if y >= 0 then x ** y else 1.0 / x ** (abs y);
    
    words2qword : Word * Word * Word * Word -> Qword
    words2qword(w1, w2, w3, w4) ==
        w4  * 0x1000000000000 + w3  * 0x100000000 + w2  * 0x10000 + w1;
    
    qword2words : Qword -> Word * Word * Word * Word
    qword2words(i) ==
        let
            byte1 = i mod 0x10000,
            i1 = i div 0x10000,
            byte2 = i1 mod 0x10000,
            i2 = i1 div 0x10000,
            byte3 = i2 mod 0x10000,
            byte4 = i2 div 0x10000
        in mk_(byte1, byte2, byte3, byte4);
    
    int2qword : int -> Qword
    int2qword(x) == if x < 0 then x + 0x10000000000000000 else x;
    
    qword2int : Qword -> int
    qword2int(x) ==
        if x < 0x8000000000000000 then x else x - 0x10000000000000000;
    
    dword2int : Dword -> int
    dword2int(x) == if x < 0x80000000 then x else x - 0x100000000;
    
    byte2int : Byte -> int
    byte2int(x) == if x < 0x80 then x else x - 0x100;
    
    oid2int : OID -> Int
    oid2int(oid) ==
        if
            oid mod 2 = 1
        then
            (if oid <= 0x8000000000000000 then oid div 2 else oid div 2 - 0x8000000000000000)
        else
            invalidIntValue;
    
    int2oid : Int -> OID
    int2oid(i) ==
        if
            i <> invalidIntValue
        then
            (if i >= 0 then i  * 2 + 1 else (0x8000000000000000 + i)  * 2 + smallIntegerTag)
        else
            invalidOidValue;
    
    oid2float : OID -> Float
    oid2float(oid) ==
        if
            oid mod 0x100 = floatTag
        then
            (let
                    f : nat = oid div 0x100,
                    fraction : nat = f mod 0x800000,
                    signExponent : nat = f div 0x800000,
                    exponent : nat = signExponent mod 0x100,
                    sign : nat = signExponent div 0x100
                in mk_Float(sign, exponent, fraction))
        else
            invalidFloatValue;
    
    float2oid : Float -> OID
    float2oid(f) ==
        if
            f <> invalidFloatValue
        then
            f.sign  * 0x8000000000 + f.exponent  * 0x80000000
            + f.fraction  * 0x100
            + floatTag
        else
            invalidOidValue;
    
    oid2char : OID -> Char
    oid2char(oid) ==
        if oid mod 0x100 = charTag then oid div 0x100 else invalidCharValue;
    
    char2oid : Char -> OID
    char2oid(c) ==
        if c <> invalidCharValue then c  * 0x100 + charTag else invalidOidValue;
    
    oid2pointer : OID -> Pointer
    oid2pointer(oid) ==
        if oid mod 4 = 0 then oid div 0x100 else invalidPointerValue;
    
    pointer2oid : Pointer -> OID
    pointer2oid(p) ==
        if p <> invalidPointerValue then p  * 0x100 else invalidOidValue;
    
    isInt : OID -> bool
    isInt(oid) == oid mod 2 = 1;
    
    isFloat : OID -> bool
    isFloat(oid) == oid mod 0x100 = floatTag;
    
    isChar : OID -> bool
    isChar(oid) == oid mod 0x100 = charTag;
    
    isPointer : OID -> bool
    isPointer(oid) == oid mod 4 = 0;
    
    isSeq : OID -> bool
    isSeq(oid) == oid = emptySeqValue or oid mod 0x100 = seqTag;
    
    isTuple : OID -> bool
    isTuple(oid) == oid mod 0x100 = tupleTag;
    
    isRecord : OID -> bool
    isRecord(oid) == oid mod 0x100 = recordTag;
    
    isTupleRecord : OID -> bool
    isTupleRecord(oid) == oid mod 0x100 = tupleTag;

values
     char2int : map char to nat =
        {'\x00' |-> 0x00, '\x01' |-> 0x01, '\x02' |-> 0x02,
        '\x03' |-> 0x03, '\x04' |-> 0x04, '\x05' |-> 0x05,
        '\x06' |-> 0x06, '\x07' |-> 0x07, '\x08' |-> 0x08,
        '\x09' |-> 0x09, '\x0a' |-> 0x0a, '\x0b' |-> 0x0b,
        '\x0c' |-> 0x0c, '\x0d' |-> 0x0d, '\x0e' |-> 0x0e,
        '\x0f' |-> 0x0f, '\x10' |-> 0x10, '\x11' |-> 0x11,
        '\x12' |-> 0x12, '\x13' |-> 0x13, '\x14' |-> 0x14,
        '\x15' |-> 0x15, '\x16' |-> 0x16, '\x17' |-> 0x17,
        '\x18' |-> 0x18, '\x19' |-> 0x19, '\x1a' |-> 0x1a,
        '\x1b' |-> 0x1b, '\x1c' |-> 0x1c, '\x1d' |-> 0x1d,
        '\x1e' |-> 0x1e, '\x1f' |-> 0x1f, '\x20' |-> 0x20,
        '\x21' |-> 0x21, '\x22' |-> 0x22, '\x23' |-> 0x23,
        '\x24' |-> 0x24, '\x25' |-> 0x25, '\x26' |-> 0x26,
        '\x27' |-> 0x27, '\x28' |-> 0x28, '\x29' |-> 0x29,
        '\x2a' |-> 0x2a, '\x2b' |-> 0x2b, '\x2c' |-> 0x2c,
        '\x2d' |-> 0x2d, '\x2e' |-> 0x2e, '\x2f' |-> 0x2f,
        '\x30' |-> 0x30, '\x31' |-> 0x31, '\x32' |-> 0x32,
        '\x33' |-> 0x33, '\x34' |-> 0x34, '\x35' |-> 0x35,
        '\x36' |-> 0x36, '\x37' |-> 0x37, '\x38' |-> 0x38,
        '\x39' |-> 0x39, '\x3a' |-> 0x3a, '\x3b' |-> 0x3b,
        '\x3c' |-> 0x3c, '\x3d' |-> 0x3d, '\x3e' |-> 0x3e,
        '\x3f' |-> 0x3f, '\x40' |-> 0x40, '\x41' |-> 0x41,
        '\x42' |-> 0x42, '\x43' |-> 0x43, '\x44' |-> 0x44,
        '\x45' |-> 0x45, '\x46' |-> 0x46, '\x47' |-> 0x47,
        '\x48' |-> 0x48, '\x49' |-> 0x49, '\x4a' |-> 0x4a,
        '\x4b' |-> 0x4b, '\x4c' |-> 0x4c, '\x4d' |-> 0x4d,
        '\x4e' |-> 0x4e, '\x4f' |-> 0x4f, '\x50' |-> 0x50,
        '\x51' |-> 0x51, '\x52' |-> 0x52, '\x53' |-> 0x53,
        '\x54' |-> 0x54, '\x55' |-> 0x55, '\x56' |-> 0x56,
        '\x57' |-> 0x57, '\x58' |-> 0x58, '\x59' |-> 0x59,
        '\x5a' |-> 0x5a, '\x5b' |-> 0x5b, '\x5c' |-> 0x5c,
        '\x5d' |-> 0x5d, '\x5e' |-> 0x5e, '\x5f' |-> 0x5f,
        '\x60' |-> 0x60, '\x61' |-> 0x61, '\x62' |-> 0x62,
        '\x63' |-> 0x63, '\x64' |-> 0x64, '\x65' |-> 0x65,
        '\x66' |-> 0x66, '\x67' |-> 0x67, '\x68' |-> 0x68,
        '\x69' |-> 0x69, '\x6a' |-> 0x6a, '\x6b' |-> 0x6b,
        '\x6c' |-> 0x6c, '\x6d' |-> 0x6d, '\x6e' |-> 0x6e,
        '\x6f' |-> 0x6f, '\x70' |-> 0x70, '\x71' |-> 0x71,
        '\x72' |-> 0x72, '\x73' |-> 0x73, '\x74' |-> 0x74,
        '\x75' |-> 0x75, '\x76' |-> 0x76, '\x77' |-> 0x77,
        '\x78' |-> 0x78, '\x79' |-> 0x79, '\x7a' |-> 0x7a,
        '\x7b' |-> 0x7b, '\x7c' |-> 0x7c, '\x7d' |-> 0x7d,
        '\x7e' |-> 0x7e, '\x7f' |-> 0x7f};

end Data
