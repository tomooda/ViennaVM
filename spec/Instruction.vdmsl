module Instruction
imports
    from Data all,
    from Memory all,
    from Register all,
    from ActivationRecord all,
    from CodeRecord all
exports all
definitions
values
     ERR = 0x0000;
     NOP = 0x0001;
     ALLOC = 0x0002;
     RESET = 0x0010;
     MOVE = 0x0011;
     MOVEI = 0x0012;
     ARGS = 0x0018;
     ARG1 = 0x0019;
     ARG2 = 0x001a;
     ARG3 = 0x001b;
     ARG4 = 0x001c;
     ARG5 = 0x001d;
     ARG6 = 0x001e;
     ARG7 = 0x001f;
     LOAD = 0x0020;
     LOAD1 = 0x0021;
     LOAD2 = 0x0022;
     LOAD3 = 0x0023;
     LOAD4 = 0x0024;
     LOAD5 = 0x0025;
     LOAD6 = 0x0026;
     LOAD7 = 0x0027;
     STORE = 0x0028;
     STORE1 = 0x0029;
     STORE2 = 0x002a;
     STORE3 = 0x002b;
     STORE4 = 0x002c;
     STORE5 = 0x002d;
     STORE6 = 0x002e;
     STORE7 = 0x002f;
     ADD = 0x00030;
     SUB = 0x0031;
     MUL = 0x0032;
     IDIV = 0x0033;
     IMOD = 0x0034;
     EQUAL = 0x0050;
     LESSTHAN = 0x0051;
     NOT = 0x0060;
     JUMP = 0x0070;
     JUMPTRUE = 0x0071;
     JUMPFALSE = 0x0072;
     CALL = 0x0080;
     CALL0 = 0x0081;
     CALL1 = 0x0082;
     CALLREC = 0x0083;
     CALLREC0 = 0x0084;
     CALLREC1 = 0x0085;
     CALLREC2 = 0x0086;
     RET = 0x0088;
     RETTRUE = 0x0089;
     RETFALSE = 0x008a;

operations
    step : () ==> ()
    step() ==
        let i = Data`oid2int(fetch())
        in
            if
                i <> Data`invalidIntValue
            then
                let
                    code = Data`int2qword(i),
                    mk_(opcode, reg1, reg2, reg3) = Data`qword2words(code)
                in
                    (cases opcode:
                        (ERR) ->
                            (exit Register`read_ip() - 1;
                            err("error instruction invoked")),
                        (NOP) -> nop(),
                        (ALLOC) -> alloc(reg1, reg2, reg3, fetch()),
                        (RESET) -> reset(reg1, reg2, reg3),
                        (MOVE) -> move(reg1, reg2, reg3),
                        (MOVEI) -> movei(reg1, reg2, reg3, fetch()),
                        (ARGS) -> args(reg1, reg2, reg3),
                        (ARG1) -> arg1(reg1, reg2, reg3),
                        (ARG2) -> arg2(reg1, reg2, reg3),
                        (ARG3) -> arg3(reg1, reg2, reg3),
                        (ARG4) -> arg4(reg1, reg2, reg3),
                        (ARG5) -> arg5(reg1, reg2, reg3),
                        (ARG6) -> arg6(reg1, reg2, reg3),
                        (ARG7) -> arg7(reg1, reg2, reg3),
                        (LOAD) -> load(reg1, reg2, reg3),
                        (LOAD1) -> load1(reg1, reg2, reg3),
                        (LOAD2) -> load2(reg1, reg2, reg3),
                        (LOAD3) -> load3(reg1, reg2, reg3),
                        (LOAD4) -> load4(reg1, reg2, reg3),
                        (LOAD5) -> load5(reg1, reg2, reg3),
                        (LOAD6) -> load6(reg1, reg2, reg3),
                        (LOAD7) -> load7(reg1, reg2, reg3),
                        (STORE) -> store(reg1, reg2, reg3),
                        (STORE1) -> store1(reg1, reg2, reg3),
                        (STORE2) -> store2(reg1, reg2, reg3),
                        (STORE3) -> store3(reg1, reg2, reg3),
                        (STORE4) -> store4(reg1, reg2, reg3),
                        (STORE5) -> store5(reg1, reg2, reg3),
                        (STORE6) -> store6(reg1, reg2, reg3),
                        (STORE7) -> store7(reg1, reg2, reg3),
                        (ADD) -> add(reg1, reg2, reg3),
                        (SUB) -> sub(reg1, reg2, reg3),
                        (MUL) -> mul(reg1, reg2, reg3),
                        (IDIV) -> idiv(reg1, reg2, reg3),
                        (IMOD) -> imod(reg1, reg2, reg3),
                        (LESSTHAN) -> lessthan(reg1, reg2, reg3),
                        (EQUAL) -> equal(reg1, reg2, reg3),
                        (NOT) -> bnot(reg1, reg2),
                        (JUMP) -> jump(reg1),
                        (JUMPTRUE) -> jumptrue(reg1, reg2),
                        (JUMPFALSE) -> jumpfalse(reg1, reg2),
                        (CALL) -> call(reg1, reg2, reg3),
                        (CALL0) -> call0(reg1, reg2),
                        (CALL1) -> call1(reg1, reg2, reg3),
                        (CALLREC) -> callrec(reg1, reg2),
                        (CALLREC0) -> callrec0(reg1),
                        (CALLREC1) -> callrec1(reg1, reg2),
                        (CALLREC2) -> callrec2(reg1, reg2, reg3),
                        (RET) -> ret(reg1),
                        (RETTRUE) -> rettrue(reg1, reg2),
                        (RETFALSE) -> retfalse(reg1, reg2),
                        others -> err("decode error: unknown instruction")
                        end)
            else
                err("decoder error: instruction is not integer");
    
    fetch : () ==> Data`OID
    fetch() ==
        let ip = Register`read_ip(), cr = Register`read_cr()
        in
            (Register`write_ip(ip + 1);
            if
                ip >= 0 and ip < CodeRecord`read_code_size(cr)
            then
                return CodeRecord`read_code(cr, ip)
            else
                (err("fetch error: ip out of bounds");
                return Data`invalidOidValue));
    
    err : seq of char ==> ()
    err(msg) == exit msg;
    
    nop : () ==> ()
    nop() == skip;
    
    alloc : Register`Register * Register`Register * Register`Register *
    Data`OID ==> ()
    alloc(reg1, reg2, reg3, imm) ==
        let size = Data`oid2int(imm)
        in
            if
                size <> Data`invalidIntValue
            then
                let p = Memory`alloc(size)
                in
                    if
                        p <> Data`invalidPointerValue
                    then
                        (if reg1 > 0 then Register`write_pointer(reg1, p);
                        if reg2 > 0 then Register`write_pointer(reg2, p);
                        if reg3 > 0 then Register`write_pointer(reg3, p))
                    else
                        err("alloc instruction error: allocator returned invalid pointer")
            else
                err("alloc instruction error: size is not integer");
    
    reset : Register`Register * Register`Register * Register`Register ==> ()
    reset(reg1, reg2, reg3) ==
        (if reg1 > 0 then Register`write_oid(reg1, Data`invalidOidValue);
        if reg2 > 0 then Register`write_oid(reg2, Data`invalidOidValue);
        if reg3 > 0 then Register`write_oid(reg3, Data`invalidOidValue));
    
    move : Register`Register * Register`Register * Register`Register ==> ()
    move(dst1, dst2, src) ==
        if
            src > 0
        then
            (if dst1 > 0 then Register`move(dst1, src);
            if dst2 > 0 then Register`move(dst2, src))
        else
            err("move instruction error: src register not specified");
    
    movei : Register`Register * Register`Register * Register`Register *
    Data`OID ==> ()
    movei(dst1, dst2, dst3, imm) ==
        if
            imm <> Data`invalidOidValue
        then
            (if dst1 > 0 then Register`write_oid(dst1, imm);
            if dst2 > 0 then Register`write_oid(dst2, imm);
            if dst3 > 0 then Register`write_oid(dst3, imm))
        else
            err("movei instruction error: invalid immediate value");
    
    args : Register`Register * Register`Register * Register`Register ==> ()
    args(dst1, dst2, dst3) ==
        let args = ActivationRecord`read_args(Register`read_ar())
        in
            (if dst1 > 0 then Register`write_pointer(dst1, args);
            if dst2 > 0 then Register`write_pointer(dst2, args);
            if dst3 > 0 then Register`write_pointer(dst3, args));
    
    argi : Register`Register * Register`Register * Register`Register * nat ==> ()
    argi(dst1, dst2, dst3, i) ==
        let arg = ActivationRecord`read_arg(Register`read_ar(), i)
        in
            (if dst1 > 0 then Register`write_oid(dst1, arg);
            if dst2 > 0 then Register`write_oid(dst2, arg);
            if dst3 > 0 then Register`write_oid(dst3, arg));
    
    arg1 : Register`Register * Register`Register * Register`Register ==> ()
    arg1(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 1);
    
    arg2 : Register`Register * Register`Register * Register`Register ==> ()
    arg2(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 2);
    
    arg3 : Register`Register * Register`Register * Register`Register ==> ()
    arg3(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 3);
    
    arg4 : Register`Register * Register`Register * Register`Register ==> ()
    arg4(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 4);
    
    arg5 : Register`Register * Register`Register * Register`Register ==> ()
    arg5(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 5);
    
    arg6 : Register`Register * Register`Register * Register`Register ==> ()
    arg6(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 6);
    
    arg7 : Register`Register * Register`Register * Register`Register ==> ()
    arg7(dst1, dst2, dst3) == argi(dst1, dst2, dst3, 7);
    
    loadi : Register`Register * Register`Register * Register`Register * int ==> ()
    loadi(dst, src, intReg, offset) ==
        if
            dst > 0 and src > 0
        then
            let
                pointer = Register`read_pointer(src),
                slot =
                    if intReg > 0 then Register`read_int(intReg) else 0 + offset
            in
                if
                    Memory`slots_size(pointer) >= slot
                then
                    Register`write_oid(dst, Memory`read_slot(pointer, slot))
                else
                    err("load instruction error: index out of bounds")
        else
            err("load instruction error: operands not specified");
    
    load : Register`Register * Register`Register * Register`Register ==> ()
    load(dst, src, intReg) == loadi(dst, src, intReg, 0);
    
    load1 : Register`Register * Register`Register * Register`Register ==> ()
    load1(dst, src, intReg) == loadi(dst, src, intReg, 1);
    
    load2 : Register`Register * Register`Register * Register`Register ==> ()
    load2(dst, src, intReg) == loadi(dst, src, intReg, 2);
    
    load3 : Register`Register * Register`Register * Register`Register ==> ()
    load3(dst, src, intReg) == loadi(dst, src, intReg, 3);
    
    load4 : Register`Register * Register`Register * Register`Register ==> ()
    load4(dst, src, intReg) == loadi(dst, src, intReg, 4);
    
    load5 : Register`Register * Register`Register * Register`Register ==> ()
    load5(dst, src, intReg) == loadi(dst, src, intReg, 5);
    
    load6 : Register`Register * Register`Register * Register`Register ==> ()
    load6(dst, src, intReg) == loadi(dst, src, intReg, 6);
    
    load7 : Register`Register * Register`Register * Register`Register ==> ()
    load7(dst, src, intReg) == loadi(dst, src, intReg, 7);
    
    storei : Register`Register * Register`Register * Register`Register * int ==> ()
    storei(dst, intReg, src, offset) ==
        if
            dst > 0 and src > 0
        then
            let
                pointer = Register`read_pointer(dst),
                slot =
                    if intReg > 0 then Register`read_int(intReg) else 0 + offset
            in
                if
                    Memory`slots_size(pointer) >= slot
                then
                    Memory`write_slot(pointer, slot, Register`read_oid(src))
                else
                    err("store instruction error: index out of bounds")
        else
            err("store instruction error: operands not specified");
    
    store : Register`Register * Register`Register * Register`Register ==> ()
    store(dst, intReg, src) == storei(dst, intReg, src, 0);
    
    store1 : Register`Register * Register`Register * Register`Register ==> ()
    store1(dst, intReg, src) == storei(dst, intReg, src, 1);
    
    store2 : Register`Register * Register`Register * Register`Register ==> ()
    store2(dst, intReg, src) == storei(dst, intReg, src, 2);
    
    store3 : Register`Register * Register`Register * Register`Register ==> ()
    store3(dst, intReg, src) == storei(dst, intReg, src, 3);
    
    store4 : Register`Register * Register`Register * Register`Register ==> ()
    store4(dst, intReg, src) == storei(dst, intReg, src, 4);
    
    store5 : Register`Register * Register`Register * Register`Register ==> ()
    store5(dst, intReg, src) == storei(dst, intReg, src, 5);
    
    store6 : Register`Register * Register`Register * Register`Register ==> ()
    store6(dst, intReg, src) == storei(dst, intReg, src, 6);
    
    store7 : Register`Register * Register`Register * Register`Register ==> ()
    store7(dst, intReg, src) == storei(dst, intReg, src, 7);
    
    add : Register`Register * Register`Register * Register`Register ==> ()
    add(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 = Register`read_int(src1),
                num1 : [real] =
                    if
                        int1 <> Data`invalidIntValue
                    then
                        int1
                    else
                        (let float1 = Register`read_float(src1)
                            in
                                (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil)),
                int2 = Register`read_int(src2),
                num2 : [real] =
                    if
                        int2 <> Data`invalidIntValue
                    then
                        int2
                    else
                        (let float2 = Register`read_float(src2)
                            in
                                (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil))
            in
                if
                    num1 <> nil and num2 <> nil
                then
                    let num3 : real = num1 + num2
                    in
                        if
                            int1 <> Data`invalidIntValue and int2 <> Data`invalidIntValue
                        then
                            Register`write_int(dst, num3)
                        else
                            Register`write_float(dst, Data`real2float(num3))
                else
                    err("add instruction error: arithmetic error")
        else
            err("add instruction error: operands not specified");
    
    sub : Register`Register * Register`Register * Register`Register ==> ()
    sub(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 = Register`read_int(src1),
                num1 : [real] =
                    if
                        int1 <> Data`invalidIntValue
                    then
                        int1
                    else
                        (let float1 = Register`read_float(src1)
                            in
                                (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil)),
                int2 = Register`read_int(src2),
                num2 : [real] =
                    if
                        int2 <> Data`invalidIntValue
                    then
                        int2
                    else
                        (let float2 = Register`read_float(src2)
                            in
                                (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil))
            in
                if
                    num1 <> nil and num2 <> nil
                then
                    let num3 : real = num1 - num2
                    in
                        if
                            int1 <> Data`invalidIntValue and int2 <> Data`invalidIntValue
                        then
                            Register`write_int(dst, num3)
                        else
                            Register`write_float(dst, Data`real2float(num3))
                else
                    err("sub instruction error: operands not integer")
        else
            err("sub instruction error: operands not specified");
    
    mul : Register`Register * Register`Register * Register`Register ==> ()
    mul(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 = Register`read_int(src1),
                num1 : [real] =
                    if
                        int1 <> Data`invalidIntValue
                    then
                        int1
                    else
                        (let float1 = Register`read_float(src1)
                            in
                                (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil)),
                int2 = Register`read_int(src2),
                num2 : [real] =
                    if
                        int2 <> Data`invalidIntValue
                    then
                        int2
                    else
                        (let float2 = Register`read_float(src2)
                            in
                                (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil))
            in
                if
                    num1 <> nil and num2 <> nil
                then
                    let num3 : real = num1  * num2
                    in
                        if
                            int1 <> Data`invalidIntValue and int2 <> Data`invalidIntValue
                        then
                            Register`write_int(dst, num3)
                        else
                            Register`write_float(dst, Data`real2float(num3))
                else
                    err("mul instruction error: operands not integer")
        else
            err("mul instruction error: operands not specified");
    
    fdiv : Register`Register * Register`Register * Register`Register ==> ()
    fdiv(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                num1 : [real] =
                    let float1 = Register`read_float(src1)
                    in
                        (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil),
                num2 : [real] =
                    let float2 = Register`read_float(src2)
                    in
                        (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil)
            in
                if
                    (num1 <> nil and num2 <> nil) and num2 <> 0
                then
                    Register`write_float(dst, Data`real2float(num1 / num2))
                else
                    err("fdiv instruction error: operands not number")
        else
            err("fdiv instruction error: operands not specified");
    
    idiv : Register`Register * Register`Register * Register`Register ==> ()
    idiv(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 : Data`Int = Register`read_int(src1),
                int2 : Data`Int = Register`read_int(src2)
            in
                if
                    int1 <> Data`invalidIntValue and int2 <> Data`invalidIntValue
                then
                    Register`write_int(dst, int1 div int2)
                else
                    err("idiv instruction error: operands not integer")
        else
            err("idiv instruction error: operands not specified");
    
    imod : Register`Register * Register`Register * Register`Register ==> ()
    imod(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 : Data`Int = Register`read_int(src1),
                int2 : Data`Int = Register`read_int(src2)
            in
                if
                    (int1 <> Data`invalidIntValue and int2 <> Data`invalidIntValue)
                    and dst > 0
                then
                    Register`write_int(dst, int1 mod int2)
                else
                    err("imod instruction error: operands not integer")
        else
            err("imod instruction error: operands not specified");
    
    lessthan : Register`Register * Register`Register * Register`Register ==> ()
    lessthan(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 = Register`read_int(src1),
                num1 : [real] =
                    if
                        int1 <> Data`invalidIntValue
                    then
                        int1
                    else
                        (let float1 = Register`read_float(src1)
                            in
                                (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil)),
                int2 = Register`read_int(src2),
                num2 : [real] =
                    if
                        int2 <> Data`invalidIntValue
                    then
                        int2
                    else
                        (let float2 = Register`read_float(src2)
                            in
                                (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil))
            in
                if
                    num1 <> nil and num2 <> nil
                then
                    Register`write_oid(dst, if num1 < num2 then Data`trueValue else Data`falseValue)
                else
                    err("lessthan instruction error: operands not integer")
        else
            err("lessthan instruction error: operands not specified");
    
    equal : Register`Register * Register`Register * Register`Register ==> ()
    equal(dst, src1, src2) ==
        if
            (dst > 0 and src1 > 0) and src2 > 0
        then
            let
                int1 = Register`read_int(src1),
                num1 : [real] =
                    if
                        int1 <> Data`invalidIntValue
                    then
                        int1
                    else
                        (let float1 = Register`read_float(src1)
                            in
                                (if float1 <> Data`invalidFloatValue then Data`float2real(float1) else nil)),
                int2 = Register`read_int(src2),
                num2 : [real] =
                    if
                        int2 <> Data`invalidIntValue
                    then
                        int2
                    else
                        (let float2 = Register`read_float(src2)
                            in
                                (if float2 <> Data`invalidFloatValue then Data`float2real(float2) else nil))
            in
                if
                    num1 <> nil and num2 <> nil
                then
                    Register`write_oid(dst, if num1 = num2 then Data`trueValue else Data`falseValue)
                else
                    err("equal instruction error: operands not number")
        else
            err("equal instruction error: operands not specified");
    
    bnot : Register`Register * Register`Register ==> ()
    bnot(dst, src) ==
        if
            dst > 0 and src > 0
        then
            cases Register`read_oid(src):
                (Data`trueValue) -> Register`write_oid(dst, Data`falseValue),
                (Data`falseValue) -> Register`write_oid(dst, Data`trueValue),
                others -> err("not instruction error: 2nd operand not boolean")
                end
        else
            err("not instruction error: operands not specified");
    
    jump : Register`Register ==> ()
    jump(intReg) ==
        if
            intReg > 0
        then
            let ip = Register`read_int(intReg)
            in
                if
                    ip <> Data`invalidIntValue
                then
                    Register`write_ip(ip)
                else
                    err("jump instruction error: operand not integer")
        else
            err("jump instruction error: register not specified");
    
    jumptrue : Register`Register * Register`Register ==> ()
    jumptrue(boolReg, intReg) ==
        if
            boolReg > 0 and intReg > 0
        then
            let b = Register`read_oid(boolReg), ip = Register`read_int(intReg)
            in
                if
                    ip <> Data`invalidIntValue
                then
                    cases b:
                        (Data`trueValue) -> Register`write_ip(ip),
                        (Data`falseValue) -> skip,
                        others -> err("jumptrue instruction error: operand not boolean")
                        end
                else
                    err("jumptrue instruction error: operand not integer")
        else
            err("jumptrue instruction error: operands not specified");
    
    jumpfalse : Register`Register * Register`Register ==> ()
    jumpfalse(boolReg, intReg) ==
        if
            boolReg > 0 and intReg > 0
        then
            let b = Register`read_oid(boolReg), ip = Register`read_int(intReg)
            in
                if
                    ip <> Data`invalidIntValue
                then
                    cases b:
                        (Data`trueValue) -> skip,
                        (Data`falseValue) -> Register`write_ip(ip),
                        others -> err("jumpfalse instruction error: operand not boolean")
                        end
                else
                    err("jumpfalse instruction error: operand not integer")
        else
            err("jumpfalse instruction error: operands not specified");
    
    call : Register`Register * Register`Register * Register`Register ==> ()
    call(dst, crPointerReg, argsPointerReg) ==
        if
            crPointerReg > 0
        then
            let
                args =
                    if
                        argsPointerReg > 0
                    then
                        Register`read_pointer(argsPointerReg)
                    else
                        Data`invalidPointerValue,
                cr = Register`read_pointer(crPointerReg)
            in
                if
                    cr <> Data`invalidPointerValue
                then
                    let
                        num_regs = CodeRecord`read_num_regs(cr),
                        ar =
                            ActivationRecord`alloc_ar(
                                Register`read_ar(),
                                args,
                                dst,
                                Register`read_ip(),
                                Register`read_cr(),
                                num_regs)
                    in
                        (Register`write_ar(ar);
                        Register`write_cr(cr);
                        for i = 1 to  num_regs
                        do ActivationRecord`write_local(ar, i, Register`read_oid(i)))
                else
                    err("call instruction error: second operand not pointer")
        else
            err("call instruction error: operands not specified");
    
    call0 : Register`Register * Register`Register ==> ()
    call0(dst, crPointerReg) ==
        if
            crPointerReg > 0
        then
            let
                args = Data`invalidPointerValue,
                cr = Register`read_pointer(crPointerReg)
            in
                if
                    cr <> Data`invalidPointerValue
                then
                    let
                        num_regs = CodeRecord`read_num_regs(cr),
                        ar =
                            ActivationRecord`alloc_ar(
                                Register`read_ar(),
                                args,
                                dst,
                                Register`read_ip(),
                                Register`read_cr(),
                                num_regs)
                    in
                        (Register`write_ar(ar);
                        Register`write_cr(cr);
                        for i = 1 to  num_regs
                        do ActivationRecord`write_local(ar, i, Register`read_oid(i)))
                else
                    err("call instruction error: second operand not pointer")
        else
            err("call instruction error: operands not specified");
    
    call1 : Register`Register * Register`Register * Register`Register ==> ()
    call1(dst, crPointerReg, arg1) ==
        if
            crPointerReg > 0
        then
            let
                args = Memory`alloc(1), cr = Register`read_pointer(crPointerReg)
            in
                if
                    cr <> Data`invalidPointerValue
                then
                    let
                        num_regs = CodeRecord`read_num_regs(cr),
                        ar =
                            ActivationRecord`alloc_ar(
                                Register`read_ar(),
                                args,
                                dst,
                                Register`read_ip(),
                                Register`read_cr(),
                                num_regs)
                    in
                        (Memory`write_slot(args, 1, Register`read_oid(arg1));
                        Register`write_ar(ar);
                        Register`write_cr(cr);
                        for i = 1 to  num_regs
                        do ActivationRecord`write_local(ar, i, Register`read_oid(i)))
                else
                    err("call instruction error: second operand not pointer")
        else
            err("call instruction error: operands not specified");
    
    callrec : Register`Register * Register`Register ==> ()
    callrec(dst, argsPointerReg) ==
        let
            args =
                if
                    argsPointerReg > 0
                then
                    Register`read_pointer(argsPointerReg)
                else
                    Data`invalidPointerValue,
            cr = Register`read_cr()
        in
            let
                num_regs = CodeRecord`read_num_regs(cr),
                ar =
                    ActivationRecord`alloc_ar(
                        Register`read_ar(), args, dst, Register`read_ip(), cr, num_regs)
            in
                (Register`write_ar(ar);
                Register`write_ip(0);
                for i = 1 to  num_regs
                do ActivationRecord`write_local(ar, i, Register`read_oid(i)));
    
    callrec0 : Register`Register ==> ()
    callrec0(dst) ==
        let cr = Register`read_cr()
        in
            let
                num_regs = CodeRecord`read_num_regs(cr),
                ar =
                    ActivationRecord`alloc_ar(
                        Register`read_ar(),
                        Data`invalidPointerValue,
                        dst,
                        Register`read_ip(),
                        cr,
                        num_regs)
            in
                (Register`write_ar(ar);
                Register`write_ip(0);
                for i = 1 to  num_regs
                do ActivationRecord`write_local(ar, i, Register`read_oid(i)));
    
    callrec1 : Register`Register * Register`Register ==> ()
    callrec1(dst, arg1) ==
        if
            arg1 > 0
        then
            let args = Memory`alloc(1), cr = Register`read_cr()
            in
                let
                    num_regs = CodeRecord`read_num_regs(cr),
                    ar =
                        ActivationRecord`alloc_ar(
                            Register`read_ar(), args, dst, Register`read_ip(), cr, num_regs)
                in
                    (Memory`write_slot(args, 1, Register`read_oid(arg1));
                    Register`write_ar(ar);
                    Register`write_ip(0);
                    for i = 1 to  num_regs
                    do ActivationRecord`write_local(ar, i, Register`read_oid(i)))
        else
            err("callrec1 instruction error: second operand not specified");
    
    callrec2 : Register`Register * Register`Register * Register`Register ==> ()
    callrec2(dst, arg1, arg2) ==
        if
            arg1 > 0 and arg2 > 0
        then
            let args = Memory`alloc(2), cr = Register`read_cr()
            in
                let
                    num_regs = CodeRecord`read_num_regs(cr),
                    ar =
                        ActivationRecord`alloc_ar(
                            Register`read_ar(), args, dst, Register`read_ip(), cr, num_regs)
                in
                    (Memory`write_slot(args, 1, Register`read_oid(arg1));
                    Memory`write_slot(args, 2, Register`read_oid(arg2));
                    Register`write_ar(ar);
                    Register`write_ip(0);
                    for i = 1 to  num_regs
                    do ActivationRecord`write_local(ar, i, Register`read_oid(i)))
        else
            err("callrec2 instruction error: second/third operand not specified");
    
    ret : Register`Register ==> ()
    ret(src) ==
        let ar = Register`read_ar(), cr = Register`read_cr()
        in
            if
                ar <> Data`invalidPointerValue and cr <> Data`invalidPointerValue
            then
                let
                    ret_ar = ActivationRecord`read_dynamic_link(ar),
                    ret_cr = ActivationRecord`read_return_cr(ar),
                    ret_ip = ActivationRecord`read_return_ip(ar),
                    ret_reg = ActivationRecord`read_ret_reg(ar),
                    num_regs = CodeRecord`read_num_regs(cr)
                in
                    (if
                        (src > 0 and ret_reg > 0) and src <> ret_reg
                    then
                        Register`move(ret_reg, src);
                    for i = 1 to  num_regs
                    do
                        if
                            i <> ret_reg
                        then
                            Register`write_oid(i, ActivationRecord`read_local(ar, i));
                    Register`write_cr(ret_cr);
                    Register`write_ip(ret_ip);
                    Register`write_ar(ret_ar))
            else
                err("ret instruction error: no cr/ar");
    
    rettrue : Register`Register * Register`Register ==> ()
    rettrue(boolReg, src) ==
        if
            boolReg > 0
        then
            cases Register`read_oid(boolReg):
                (Data`trueValue) ->
                    let ar = Register`read_ar(), cr = Register`read_cr()
                    in
                        if
                            ar <> Data`invalidPointerValue and cr <> Data`invalidPointerValue
                        then
                            let
                                ret_ar = ActivationRecord`read_dynamic_link(ar),
                                ret_cr = ActivationRecord`read_return_cr(ar),
                                ret_ip = ActivationRecord`read_return_ip(ar),
                                ret_reg = ActivationRecord`read_ret_reg(ar),
                                num_regs = CodeRecord`read_num_regs(cr)
                            in
                                (if
                                    (src > 0 and ret_reg > 0) and src <> ret_reg
                                then
                                    Register`move(ret_reg, src);
                                for i = 1 to  num_regs
                                do
                                    if
                                        i <> ret_reg
                                    then
                                        Register`write_oid(i, ActivationRecord`read_local(ar, i));
                                Register`write_cr(ret_cr);
                                Register`write_ip(ret_ip);
                                Register`write_ar(ret_ar))
                        else
                            err("ret instruction error: no cr/ar"),
                (Data`falseValue) -> skip,
                others -> err("rettrue instruction error: operand1 not boolean")
                end
        else
            err("rettrue instruction error: operand1 not specified");
    
    retfalse : Register`Register * Register`Register ==> ()
    retfalse(boolReg, src) ==
        if
            boolReg > 0
        then
            cases Register`read_oid(boolReg):
                (Data`falseValue) ->
                    let ar = Register`read_ar(), cr = Register`read_cr()
                    in
                        if
                            ar <> Data`invalidPointerValue and cr <> Data`invalidPointerValue
                        then
                            let
                                ret_ar = ActivationRecord`read_dynamic_link(ar),
                                ret_cr = ActivationRecord`read_return_cr(ar),
                                ret_ip = ActivationRecord`read_return_ip(ar),
                                ret_reg = ActivationRecord`read_ret_reg(ar),
                                num_regs = CodeRecord`read_num_regs(cr)
                            in
                                (if src > 0 and ret_reg > 0 then Register`move(ret_reg, src);
                                for i = 1 to  num_regs
                                do
                                    if
                                        i <> ret_reg
                                    then
                                        Register`write_oid(i, ActivationRecord`read_local(ar, i));
                                Register`write_cr(ret_cr);
                                Register`write_ip(ret_ip);
                                Register`write_ar(ret_ar))
                        else
                            err("ret instruction error: no cr/ar"),
                (Data`trueValue) -> skip,
                others -> err("rettrue instruction error: operand1 not boolean")
                end
        else
            err("rettrue instruction error: operand1 not specified");

end Instruction
