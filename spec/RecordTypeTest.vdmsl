module RecordTypeTest
imports
    from Data all,
    from Memory all,
    from Equality all,
    from UnitTesting
        operations
            assertEquals renamed assertEquals;
            assert renamed assert;,
    from RecordType all
exports all
definitions
operations
    makeRecordType : seq of bool ==> Data`Pointer
    makeRecordType(flags) ==
        let p : Data`Pointer = RecordType`alloc(len flags)
        in
            (for i = 1 to  len flags
            do RecordType`write_equality_visibility(p, i, flags(i));
            return p)
    pre  len flags > 0;
    
    test_alloc : () ==> ()
    test_alloc() ==
        let p1 = RecordType`alloc(1), p2 = RecordType`alloc(2)
        in
            (assertEquals(RecordType`fields_size(p1), 1, "fields_size(alloc(1)) = 1");
            assertEquals(
                RecordType`read_equality_visibility(p1, 1),
                true,
                "field 1 is visible by default");
            assertEquals(RecordType`fields_size(p2), 2, "fields_size(alloc(2)) = 2");
            assertEquals(
                RecordType`read_equality_visibility(p2, 1),
                true,
                "field 1 is visible by default");
            assertEquals(
                RecordType`read_equality_visibility(p2, 2),
                true,
                "field 2 is visible by default"));
    
    test_read_write_equality_visibility : () ==> ()
    test_read_write_equality_visibility() ==
        let p = RecordType`alloc(2)
        in
            (assertEquals(
                RecordType`read_equality_visibility(p, 1),
                true,
                "field 1 is visible by default");
            assertEquals(
                RecordType`read_equality_visibility(p, 2),
                true,
                "field 2 is visible by default");
            RecordType`write_equality_visibility(p, 1, false);
            assertEquals(
                RecordType`read_equality_visibility(p, 1),
                false,
                "field 1 is set invisible. field 1 is invisible");
            assertEquals(
                RecordType`read_equality_visibility(p, 2),
                true,
                "field 1 is set invisible. field 2 is visible");
            RecordType`write_equality_visibility(p, 2, false);
            assertEquals(
                RecordType`read_equality_visibility(p, 1),
                false,
                "field 2 is set invisible. field 1 is invisible");
            assertEquals(
                RecordType`read_equality_visibility(p, 2),
                false,
                "field 2 is set invisible. field 2 is invisible"));
    
    test_equal : () ==> ()
    test_equal() ==
        let
            p1 = makeRecordType([true]),
            oid1 = RecordType`pointer2oid(p1),
            p2 = makeRecordType([true]),
            oid2 = RecordType`pointer2oid(p2)
        in
            (RecordType`write_equality_visibility(p1, 1, true);
            RecordType`write_equality_visibility(p2, 1, true);
            assertEquals(
                RecordType`equal(p1, p1),
                true,
                "p1 and p1 are the same object and thus are equal");
            assertEquals(
                Equality`equal(oid1, oid1),
                true,
                "oid1 and oid2 are the same object and thus are equal");
            assertEquals(
                RecordType`equal(p1, p2),
                false,
                "p1 and p2 have the same content but are not equal");
            assertEquals(
                Equality`equal(oid1, oid2),
                false,
                "oid1 and oid2 have the same content but are not equal");
            Memory`release(p1);
            Memory`release(p2));
    
    test_hash : () ==> ()
    test_hash() ==
        let
            p1 = RecordType`alloc(1),
            oid1 = RecordType`pointer2oid(p1),
            p2 = RecordType`alloc(1),
            oid2 = RecordType`pointer2oid(p2)
        in
            (RecordType`write_equality_visibility(p1, 1, true);
            RecordType`write_equality_visibility(p2, 1, true);
            assertEquals(
                RecordType`hash(p1),
                RecordType`hash(p1),
                "p1 and p1 are the same object and thus make the same hash");
            assertEquals(
                RecordType`hash(p1),
                Equality`hash(oid1),
                "Equality`hash(oid1) uses RecordType`hash(p1)");
            assertEquals(
                RecordType`hash(p2),
                Equality`hash(oid2),
                "Equality`hash(oid2) uses RecordType`hash(p2)");
            Memory`release(p1);
            Memory`release(p2));

end RecordTypeTest
